#!/usr/bin/env perl
use warnings;
use strict;

use FindBin qw($Bin);
use lib $Bin;
use x3dna_utils;

# This script is used to extract the x-, y- and z- coordinates of the
# P, O1P and O2P atoms with reference to its "middle-frame" in a
# dinucleotide step.

# First version: Monday, July 26, 2004
#      Modified: Tuesday, August 3, 2004
#
# August 2, 2004: add 'use warnings; use strict;', and corresponding
#                 code revision

# For each double helical nucleic acid structures analyzed by 3DNA,
# there is a default output file named "auxiliary.par", containing
# detailed information of various parameters, including O1P and O2P
# x-, y-, and z-coordinates expressed in the "middle-frame" of a
# dinucleotide step

# Some basic command processing ...
my $ok = 1;
my $inpfile;
my $outfile;

if ( @ARGV == 1 ) {
    if ( $ARGV[0] =~ /-/ ) {
        $ok = 0;
    } else {
        $inpfile = "auxiliary.par";
        $outfile = $ARGV[0];
    }
} elsif ( @ARGV == 2 ) {
    $inpfile = $ARGV[0];
    $outfile = $ARGV[1];
} else {
    $ok = 0;
}

if ( !$ok ) {
    print <<OP_MXYZ;
===========================================================================
SYNOPSIS
    OP_Mxyz [INPFILE] OUTFILE
DESCRIPTION
    Extract x-, y- and z-coordinates of the O1P and O2P atoms with regard to
    its "middle-reference-frame". The default input file, "auxiliary.par",
    is the one generated by "analyze".
EXAMPLES
    find_pair -t bdl084.pdb stdout | analyze
    OP_Mxyz bdl084_O1P_O2P
AUTHOR
    $author
    $url
===========================================================================
OP_MXYZ
    die "\n";
}

# Search and output ...
open( FINP, $inpfile )    || die "Can't open $inpfile for reading: $!\n";
open( FOUT, ">$outfile" ) || die "Can't open $outfile for writing: $!\n";

my $done = 0;
while ( my $str = <FINP> ) {
    if ( !$done && $str =~ /O1P atoms w\.r\.t\./ ) {    # w.r.t.
        print FOUT $str;
        my $star = 0;
        while ( $str = <FINP> ) {
            $star++ if ( $str =~ /\*{6}/ );
            print FOUT $str;
            if ( $star >= 4 ) {
                $done = 1;
                last;
            }
        }
    }
}

close(FINP);
close(FOUT);
